\chapter*{Дополнения (Bonus tracks)} %01:52:52:00 - Дополнения (Bonus tracks)

\section{Постфиксные циклы}
Чтобы с постфиксным циклом работали \verb|next| и \verb|redo|, необходимо добавить внутренный блок:
\begin{minted}{perl}
do {{
	next if $cond1;
	redo if $cond2;
	last if $cond3;
	...
}} while ( EXPR );
\end{minted}
Но в таком случае все еще не будет работать \verb|last|. Для того, чтобы работал {last}, необходимо добавлять внешний блок:
\begin{minted}{perl}
{
	do {
		next if $cond1;
		redo if $cond2;
		last if $cond3;
		...
	} while ( EXPR );
}
\end{minted}
Но в таком случае \verb|next| и \verb|redo| будут работать не так, как ожидается. Чтобы работали и \verb|next|, и \verb|redo|, и \verb|last| можно сделать следующим образом:
\begin{minted}{perl}
LOOP: {
	do {{
		next if $cond1;
		redo if $cond2;
		last LOOP if $cond3;
		...
	}} while ( EXPR );
}
\end{minted}


\section{Особенности perl 5.20}
Одна из особенностей синтаксиса в Perl~$5.20$ заключается в том, что была введена {постфиксная нотация для разыменования ссылки}:
\begin{minted}{perl}
use feature 'postderef';
no warnings 'experimental::postderef';

$sref->$*;  # same as  ${ $sref }
$aref->@*;  # same as  @{ $aref }
$aref->$#*; # same as $#{ $aref }
$href->%*;  # same as  %{ $href }
$cref->&*;  # same as  &{ $cref }
$gref->**;  # same as  *{ $gref }
\end{minted}
Еще одна особенность Perl~$5.20$~--- {хэшовый срез} (ключ/значение):
\begin{minted}{perl}
%hash = (
	key1 => "value1",
	key2 => "value2",
	key3 => "value3",
	key4 => "value4",
);
#%sub = (
#    key1 => $hash{key1},
#    key3 => $hash{key3},
#);
%sub = %hash{"key1","key3"};
       ^    ^             ^
       |    +-------------+--- на хэше
       +----- хэш-срез
\end{minted}
Аналогично введен {срез ключ/значение на массиве}:
\begin{minted}{perl}
@array = (
    "value1",
    "value2",
    "value3",
    "value4",
);
#%sub = (
#    1 => $array[1],
#    3 => $array[3],
#);
%sub = %array[ 1, 3 ];
       ^     ^      ^
       |     +------+--- на массиве
       +----- хэш-срез
\end{minted}
Другая особенность данной версии~--- {постфиксный срез} (аналогично постфиксным разыменованиям):
\begin{minted}{perl}
($a,$b) = $aref->@[ 1,3 ];
($a,$b) = @{ $aref }[ 1,3 ];

($a,$b) = $href->@{ "key1", "key2" };
($a,$b) = @{ $href }{ "key1","key2" };

%sub = $aref->%[ 1,3 ];
%sub = %{ $aref }[1,3];
%sub = (1 => $aref->[1], 3 => $aref->[3]);

%sub = $href->%{ "k1","k3" };
%sub = %{ $href }["k1","k3"];
%sub = (k1 => $href->{k1},
        k3 => $href->{k3});
\end{minted}
Кроме того, в Perl~$5.20$ сделаны {сигнатуры функций}:
\begin{minted}{perl}
use feature 'signatures';

sub foo ($x, $y) {
    return $x**2+$y;
}

sub foo {
   die "Too many arguments for subroutine"
       unless @_ <= 2;
   die "Too few arguments for subroutine"
       unless @_ >= 2;
   my $x = $_[0];
   my $y = $_[1];

   return $x**2 + $y;
}
\end{minted}

\section{Именованный унарный оператор}
{Именованные унарные операторы}~--- это функции, имеющий строго один аргумент (большинство функций в Perl):
\begin{minted}{perl}
chdir $foo    || die; # (chdir $foo) || die
chdir($foo)   || die; # (chdir $foo) || die
chdir ($foo)  || die; # (chdir $foo) || die
chdir +($foo) || die; # (chdir $foo) || die

rand 10 * 20;         # rand (10 * 20) 
rand(10) * 20;        # (rand 10) * 20
rand (10) * 20;       # (rand 10) * 20
rand +(10) * 20;      # rand (10 * 20) 

-e($file)."ext"       # -e( ($file)."ext" )
\end{minted}
Такие функции имеют очень высокий приоритет. Они трактуются как оператор над одним аргументом.

\section{Список документации}
\begin{itemize}
	\item \href{http://perldoc.perl.org/perlsyn.html}{perlsyn}: Perl syntax
	\item \href{http://perldoc.perl.org/perldata.html}{perldata}: Perl data types
	\item \href{http://perldoc.perl.org/perlref.html}{perlref}: Perl references and nested data structures
	\item \href{http://perldoc.perl.org/perllol.html}{perllol}: Manipulating Arrays of Arrays in Perl
	\item \href{http://perldoc.perl.org/perlsub.html}{perlsub}: Perl subroutines
	\item \href{http://perldoc.perl.org/perlfunc.html}{perlfunc}: Perl builtin functions
	\item \href{http://perldoc.perl.org/perlop.html}{perlop}: Perl operators and precedence
	\item \href{http://perldoc.perl.org/perlglossary.html}{perlglossary}: Perl Glossary
	\item \href{https://metacpan.org/pod/perlsecret}{perlsecret}: Perl secret operators and constants
\end{itemize}
